CREATE OR REPLACE PROCEDURE PENALIZZA_AUTO(
    I_ID_SESSIONE SESSIONE.ID%TYPE,
    I_NUMERO_AUTO AUTO.NUMERO%TYPE
) IS
    V_CONTA_AUTO        NUMBER;
    V_CONTA_ISCRIZIONI  NUMBER;
    V_CONTA_SESSIONE    NUMBER;
    V_PESO_AUTO         AUTO.PESO_KG%TYPE;
    V_POTENZA_AUTO      AUTO.POTENZA_CV%TYPE;
    V_PESO_CATEGORIA    CATEGORIA.PESO_MINIMO_KG%TYPE;
    V_POTENZA_CATEGORIA CATEGORIA.POTENZA_MASSIMA_CV%TYPE;
    V_CONTA_PASSAGGI    NUMBER;
    V_VIOLAZIONI        NUMBER := 0;
BEGIN
 
    -- ERRRORI: Auto non trovata, auto non partecipante alla sessione, sessione non trovata, peso e potenza nei limiti di regolamento, nessun passaggio nella sessione
    SELECT
        COUNT(*) INTO V_CONTA_AUTO
    FROM
        AUTO
    WHERE
        AUTO.NUMERO = I_NUMERO_AUTO;
    IF V_CONTA_AUTO = 0 THEN
        RAISE_APPLICATION_ERROR(-20000, 'Auto non trovata');
    END IF;

    SELECT
        COUNT(*) INTO V_CONTA_SESSIONE
    FROM
        SESSIONE
    WHERE
        SESSIONE.ID = I_ID_SESSIONE;
    IF V_CONTA_SESSIONE = 0 THEN
        RAISE_APPLICATION_ERROR(-20000, 'Sessione non trovata');
    END IF;

    SELECT
        COUNT(*) INTO V_CONTA_ISCRIZIONI
    FROM
        ISCRIZIONE
    WHERE
        ISCRIZIONE.ID_SESSIONE = I_ID_SESSIONE
        AND ISCRIZIONE.NUMERO_AUTO = I_NUMERO_AUTO;
    IF V_CONTA_ISCRIZIONI = 0 THEN
        RAISE_APPLICATION_ERROR(-20000, 'Auto non iscritta alla sessione');
    END IF;

    SELECT
        AUTO.PESO_KG,
        AUTO.POTENZA_CV,
        CATEGORIA.PESO_MINIMO_KG,
        CATEGORIA.POTENZA_MASSIMA_CV INTO V_PESO_AUTO,
        V_POTENZA_AUTO,
        V_PESO_CATEGORIA,
        V_POTENZA_CATEGORIA
    FROM
        CATEGORIA
        JOIN AUTO
        ON CATEGORIA.ENTE_CERTIFICAZIONE = AUTO.ENTE_CATEGORIA
        AND CATEGORIA.NOME = AUTO.NOME_CATEGORIA
    WHERE
        AUTO.NUMERO = I_NUMERO_AUTO;
    IF V_PESO_AUTO < V_PESO_CATEGORIA THEN
        V_VIOLAZIONI := V_VIOLAZIONI + 1;
    END IF;

    IF V_POTENZA_AUTO > V_POTENZA_CATEGORIA THEN
        V_VIOLAZIONI := V_VIOLAZIONI + 1;
    END IF;

    IF V_VIOLAZIONI = 0 THEN
        RAISE_APPLICATION_ERROR(-20000, 'Auto conforme ai regolamenti, impossibile penalizzare');
    END IF;

    INSERT INTO PENALITA(
        NUMERO_AUTO,
        PUNTI_PENALITA,
        DATA_PENALIZZAZIONE
    ) VALUES(
        I_NUMERO_AUTO,
        V_VIOLAZIONI * 3,
        SYSDATE
    );
END;
